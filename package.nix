{
  lib,
  stdenv,
  meson,
  ninja,
  pkg-config,
  protobuf,
  grpc,
  openssl,
  prometheus-cpp,
  zstd,
  curl,
  nlohmann_json,
  python3,
  # Nix component libraries. When building the client plugin these must be
  # ABI-compatible with the `nix` binary that will dlopen() the .so; the NixOS
  # client module passes `config.nix.package.libs.*` here for that reason.
  nix-store,
  nix-util,
}:

stdenv.mkDerivation {
  pname = "nix-grpc-store";
  version = lib.fileContents ./.version;
  src = lib.fileset.toSource {
    root = ./.;
    fileset = lib.fileset.unions [
      ./.clang-tidy
      ./.version
      ./meson.build
      ./meson.options
      ./proto
      ./src
      ./fuzz
      ./tests/farm-mock.py
      ./tests/farm-client-test.cc
      ./tests/farm-client-test.sh
    ];
  };

  doCheck = true;
  nativeCheckInputs = [ python3 ];

  nativeBuildInputs = [
    meson
    ninja
    pkg-config
    protobuf
    grpc
  ];

  buildInputs = [
    grpc
    openssl
    protobuf
    prometheus-cpp
    zstd
    nix-store
    nix-util
    curl
    nlohmann_json
  ];

  # Frame pointers + symbols so `perf` in the VM test can attribute samples
  # inside the plugin and daemon.
  env.NIX_CFLAGS_COMPILE = "-fno-omit-frame-pointer -g";
  dontStrip = true;

  meta = {
    description = "gRPC transport for the Nix remote store protocol";
    mainProgram = "nix-grpc-daemon";
    license = lib.licenses.mit;
  };
}
